pipeline {
  agent { label 'linux' }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 45, unit: 'MINUTES')
    /* make project dir the same for consisten nix src */
    checkoutToSubdirectory('status-react')
    /* Limit builds retained */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '20',
      artifactNumToKeepStr: '10',
    ))
  }

  parameters {
    string(
      name: 'BUILD_TYPE',
      description: 'Specify build type. Values: pr / e2e / nightly / release',
      defaultValue: 'pr',
    )
  }

  environment {
    /* change workspace to keep it consistent for nix src */
    SRC = "${WORKSPACE}/status-react"
    /* locale */
    LANG     = "en_US.UTF-8"
    LC_ALL   = "en_US.UTF-8"
    LANGUAGE = "en_US.UTF-8"
    TARGET_OS = 'android'
    NIX_CONF_DIR = "${SRC}/nix"
    FASTLANE_DISABLE_COLORS = 1
    REALM_DISABLE_ANALYTICS = 1
    /* since we are mounting it we need to specify location */
    STATUS_RELEASE_STORE_FILE = '/home/jenkins/status-im.keystore'
    /* We use EXECUTOR_NUMBER to avoid multiple instances clashing */
    LEIN_HOME         = "/var/tmp/lein-${EXECUTOR_NUMBER}"
    YARN_CACHE_FOLDER = "/var/tmp/yarn-${EXECUTOR_NUMBER}"
    GRADLE_USER_HOME  = "/var/tmp/gradle-${EXECUTOR_NUMBER}"
  }

  stages {
    stage('Prep') {
      steps { dir(SRC) {
        script {
          /* Necessary to load methods */
          mobile = load('ci/mobile.groovy')
          cmn    = load('ci/common.groovy')
          btype  = cmn.utils.getBuildType()
          print "Running ${btype} build!"
          cmn.ci.abortPreviousRunningBuilds()
          /* Cleanup and Prep */
          cmn.prep(btype)
        }
      } }
    }
    stage('Lint') {
      steps { dir(SRC) {
        script { cmn.nix.shell('lein cljfmt check') }
      } }
    }
    stage('Tests') {
      steps { dir(SRC) {
        script { cmn.nix.shell('lein test-cljs') }
      } }
    }
    stage('Build') {
      steps { dir(SRC) {
        script { cmn.nix.shell('lein prod-build-android')}
      } }
    }
    stage('Bundle') {
      steps { dir(SRC) {
        script { apk = mobile.android.bundle() }
      } }
    }
    stage('Archive') {
      steps { dir(SRC) {
        archiveArtifacts apk
      } }
    }
    stage('Upload') {
      steps { dir(SRC) {
        script {
          env.PKG_URL = cmn.utils.uploadArtifact(apk)
          /* build type specific */
          switch (btype) {
            case 'release':
              mobile.android.uploadToPlayStore(); break;
            case 'nightly':
              env.DIAWI_URL = mobile.android.uploadToDiawi(); break;
            case 'e2e':
              env.SAUCE_URL = mobile.android.uploadToSauceLabs()
          }
        }
      } }
    }
    stage('Cleanup') {
      steps { dir(SRC) {
        sh 'make clean'
      } }
    }
  }
  post {
    success { dir(SRC) { script {
      load('ci/common.groovy').notifyPR(true)
    } } }
    failure { dir(SRC) { script {
      load('ci/common.groovy').notifyPR(false)
    } } }
  }
}
